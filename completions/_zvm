#compdef zvm

if ! command -v zvm &> /dev/null; then
    return
fi

local -a zvm_commands
local -a zvm_aliases
local -a zvm_global_options
local -a zvm_list_options
local -a zvm_install_options

local -a zvm_local_versions
local -a zvm_remote_versions

zvm_commands=(
    'list:List versions'
    'use:Use installed version'
    'install:Install version'
    'uninstall:Uninstall version'
    'prune:Uninstall all unused versions'
    'help:Show help'
)

zvm_aliases=(
    'ls:List versions'
    'i:Install version'
    'rm:Uninstall version'
    'h:Show help'
)

zvm_global_options=(
    '(-h --help)'{-h,--help}'[Show help]'
    '(-v --verbose)'{-v,--verbose}'[Show verbose output]'
)

zvm_list_options=(
    '(-i --installed)'{-i,--installed}'[List installed versions only]'
)

zvm_install_options=(
    '(-u,--use)'{-u,--use}'[Make version active after installing]'
)

_arguments -C \
    $zvm_global_options \
    "1: :{_describe 'command' zvm_commands -- zvm_aliases}" \
    "*::arg:->args"

case $line[1] in
    ls | list)
        _arguments $zvm_list_options
        ;;
    i | install)
        zvm_remote_versions=(${(f)"$(zvm list)"})
        _arguments \
            $zvm_install_options \
            "*:: :{_describe 'command' zvm_remote_versions}"
        ;;
    rm | uninstall | use)
        zvm_local_versions=(${(f)"$(zvm list --installed)"})
        _arguments \
            "1: :{_describe 'command' zvm_local_versions}"
        ;;
esac
